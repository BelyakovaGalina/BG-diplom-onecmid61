#Область СлужебныеПроцедурыИФункции

//Отправка сообщений ботом в телеграмм
//Параметры:
//	ТекстСообщенияДляОтправки - Строка
//Возвращаемое значение:
//	Число
Функция ОтправкаСообщенияВГруппу(ТекстСообщенияДляОтправки) Экспорт
	КодСостояния = 0;
	
	НастройкиПодключения = НастройкиПодключения();

	Если НастройкиПодключения = Неопределено Тогда
		Возврат КодСостояния;
	КонецЕсли;

	Попытка
		Соединение = УстановитьСоединение(НастройкиПодключения);
	Исключение		
		ЗаписьЖурналаРегистрации("СоединениеСТелеграм",УровеньЖурналаРегистрации.Ошибка,,,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат КодСостояния;
		//ВызватьИсключение;
	КонецПопытки;

	Ресурс = СтрШаблон("bot%1/sendMessage", НастройкиПодключения.ТокенБота);
	
	ДанныеДляОтправки = Новый Структура;
	ДанныеДляОтправки.Вставить("chat_id", НастройкиПодключения.ИДГруппы);
	ДанныеДляОтправки.Вставить("text", ТекстСообщенияДляОтправки);
	
	Попытка
		ТелоОтвета = ЗаписатьЗначениеJSON(ДанныеДляОтправки);
		Заголовки = Новый Соответствие;
		Заголовки.Вставить("Content-Type", "application/json");

		Запрос = Новый HTTPЗапрос(Ресурс, Заголовки);

		Запрос.УстановитьТелоИзСтроки(ТелоОтвета);

		Ответ = Соединение.ОтправитьДляОбработки(Запрос);
	Исключение		
		ЗаписьЖурналаРегистрации("УведомлениеВТелеграм",УровеньЖурналаРегистрации.Ошибка,,,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат КодСостояния;
		//ВызватьИсключение;
	КонецПопытки;
		
	КодСостояния = Ответ.КодСостояния;

	Если КодСостояния <> 200 Тогда
		ТекстОтвета = Ответ.ПолучитьТелоКакСтроку();
		ШаблонСообщения = НСтр("ru = 'Ошибка отправки сообщения в Телеграм: код = %1, текст ответа = %2'");
		ТекстОшибки = СтрШаблон(ШаблонСообщения, КодСостояния, ТекстОтвета);

		ЗаписьЖурналаРегистрации("УведомлениеВТелеграм",УровеньЖурналаРегистрации.Ошибка,,,ТекстОшибки);
	КонецЕсли;
	
	Возврат КодСостояния;
	
КонецФункции

Функция НастройкиПодключения()               

	Результат = Новый Структура;
	Результат.Вставить("ЗащищенноеСоединение");
	Результат.Вставить("АдресТелеграм");
	Результат.Вставить("ТокенБота");
	Результат.Вставить("ИДГруппы");
	
	Результат.ЗащищенноеСоединение = Новый ЗащищенноеСоединениеOpenSSL;
	
	Результат.АдресТелеграм = "api.telegram.org";
	Результат.ТокенБота = Константы.ВКМ_ТокенУправленияТелеграмБотом.Получить();
	Результат.ИДГруппы = Константы.ВКМ_ИдентификаторГруппыДляОповещения.Получить();
	
	Возврат Результат; 
	
КонецФункции

Функция УстановитьСоединение(НастройкиПодключения)
	Возврат Новый HTTPСоединение(НастройкиПодключения.АдресТелеграм,443,,,,,
		НастройкиПодключения.ЗащищенноеСоединение);
КонецФункции


Процедура УведомленияТелеграмБоту() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_УведомленияТелеграмБоту.Ссылка,
		|	ВКМ_УведомленияТелеграмБоту.ТекстСообщения
		|ИЗ
		|	Справочник.ВКМ_УведомленияТелеграмБоту КАК ВКМ_УведомленияТелеграмБоту";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Код = ОтправкаСообщенияВГруппу(Выборка.ТекстСообщения);
	
		Если Код = 200 Тогда
			СправочникОбъект = Выборка.Ссылка.ПолучитьОбъект();
			СправочникОбъект.Удалить();
		КонецЕсли;
			
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти