
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Функция ФормированиеДокументовНаСервере(Знач Период) Экспорт

	ВыборкаДоговоров = ВыбратьДействующиеВПериодеДоговора(Период);

	СписокОтработанныхДоговоров = Новый Массив;

	Если ВыборкаДоговоров.Количество() = 0 Тогда
		ОбщегоНазначения.СообщитьПользователю("Не отобраны договора по абонентскому обслуживанию для формирования");
		Возврат Неопределено;
	КонецЕсли;
	
	Пока ВыборкаДоговоров.Следующий() Цикл
	
		НовыйДокумент = Документы.РеализацияТоваровУслуг.СоздатьДокумент();
		НовыйДокумент.Дата = КонецМесяца(КонецДня(Период));

		СсылкаНового = Документы.РеализацияТоваровУслуг.ПолучитьСсылку();
		НовыйДокумент.УстановитьСсылкуНового(СсылкаНового);
		
		НовыйДокумент.Заполнить(СсылкаНового);
		
		ЗаполнитьЗначенияСвойств(НовыйДокумент, ВыборкаДоговоров);
		
		ВыполненоАвтозаполнение = НовыйДокумент.ВКМ_ВыполнитьАвтозаполнение(ВыборкаДоговоров.СуммаАбонентскойПлаты);
		
		Если ВыполненоАвтозаполнение Тогда
			
			НовыйДокумент.СуммаДокумента = НовыйДокумент.Услуги.Итог("Сумма");
			
			Если НовыйДокумент.ПроверитьЗаполнение() Тогда
				
				НовыйДокумент.Записать(РежимЗаписиДокумента.Запись);
				
				СтрокаТаблицы = Новый Структура();
				СтрокаТаблицы.Вставить("Договор", ВыборкаДоговоров.Договор);
				СтрокаТаблицы.Вставить("Документ", НовыйДокумент.Ссылка);
				
				СписокОтработанныхДоговоров.Добавить(СтрокаТаблицы);
					
			КонецЕсли;
		Иначе
			Прервать;
		КонецЕсли;
				
	КонецЦикла;
	
	Возврат СписокОтработанныхДоговоров;

КонецФункции

Функция ВыбратьДействующиеВПериодеДоговора(Знач Период) 
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДоговорыКонтрагентов.Ссылка КАК Договор,
		|	ДоговорыКонтрагентов.ВКМ_СуммаАбонентскойПлаты КАК СуммаАбонентскойПлаты,
		|	ЕСТЬNULL(РеализацияТоваровУслуг.Ссылка, ЗНАЧЕНИЕ(Документ.РеализацияТоваровУслуг.ПустаяСсылка)) КАК
		|		ДокументРеализация,
		|	ДоговорыКонтрагентов.Владелец КАК Контрагент,
		|	ДоговорыКонтрагентов.Организация КАК Организация
		|ИЗ
		|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
		|		ПО РеализацияТоваровУслуг.Договор = ДоговорыКонтрагентов.Ссылка
		|		И РеализацияТоваровУслуг.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|ГДЕ
		|	ДоговорыКонтрагентов.ВКМ_НачалоДействияДоговора <= &КонецПериода
		|	И ДоговорыКонтрагентов.ВКМ_ОкончаниеДействияДоговора >= &НачалоПериода
		|	И НЕ ДоговорыКонтрагентов.ПометкаУдаления
		|	И ДоговорыКонтрагентов.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.ВКМ_АбоненскоеОбслуживание)
		|	И РеализацияТоваровУслуг.Ссылка ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("НачалоПериода", НачалоДня(НачалоМесяца(Период)));
	Запрос.УстановитьПараметр("КонецПериода", КонецДня(КонецМесяца(Период)));
	
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

#КонецОбласти

#КонецЕсли
