
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сформировать(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.Период) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Не задан период",,"Период","Объект.Период");
		Возврат;	
	КонецЕсли;
	
	РезультатОперации = СформироватьНаСервере();
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.Заголовок = "Фоновое выполнение задания";
	ПараметрыОжидания.ВыводитьСообщения = Истина;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОповещениеОЗавершении",ЭтотОбъект); 
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(РезультатОперации,ОповещениеОЗавершении,ПараметрыОжидания);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция СформироватьНаСервере()

	Результат = ДлительныеОперации.ВыполнитьФункцию(УникальныйИдентификатор,
			"Обработки.ВКМ_МассовоеСозданиеАктов.ФормированиеДокументовНаСервере", Объект.Период);

	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОповещениеОЗавершении(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = Неопределено Тогда
      Возврат;
	КонецЕсли;

	ЗначениеРезультата = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);

	Если Результат.Статус = "Выполнено"	 Тогда              
		
		Если ТипЗнч(ЗначениеРезультата) = Тип("Массив") Тогда
			Для Каждого Элемент Из ЗначениеРезультата Цикл
				СтрокаТаблицы = Объект.СписокДоговоров.Добавить();
				СтрокаТаблицы.Договор = Элемент.Договор;
				СтрокаТаблицы.Документ = Элемент.Документ;
			КонецЦикла;
		КонецЕсли;
		
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ТекстСообщения = СтрШаблон("%1: %2", Результат.КраткоеПредставлениеОшибки, Результат.ПодробноеПредставлениеОшибки);
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);				
	КонецЕсли;		
	
КонецПроцедуры

#КонецОбласти


