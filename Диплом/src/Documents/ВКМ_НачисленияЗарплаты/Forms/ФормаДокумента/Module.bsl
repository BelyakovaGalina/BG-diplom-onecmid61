
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Заполнить(Команда)

	Если Объект.Начисления.Количество() > 0 Тогда
		ЗадатьВопросОПродолжении();
	Иначе
		ЗаполнитьНаСервере();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Асинх Процедура ЗадатьВопросОПродолжении()
	
	ТекстВопроса = НСтр("ru = 'Заполнение уже было осуществлено. Перезаписать?'"); 
	Ответ = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет);

    Если Ответ = КодВозвратаДиалога.Да Тогда
    	ЗаполнитьНаСервере();
    КонецЕсли;
    
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНаСервере()

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_ВыполненныеСотрудникомРаботыОбороты.Сотрудник КАК Сотрудник,
		|	ВКМ_ВыполненныеСотрудникомРаботыОбороты.СуммаКОплатеОборот КАК СуммаКОплатеОборот
		|ПОМЕСТИТЬ ВТ_ОплатаРабот
		|ИЗ
		|	РегистрНакопления.ВКМ_ВыполненныеСотрудникомРаботы.Обороты(&НачалоПериода, &КонецПериода, Месяц,) КАК
		|		ВКМ_ВыполненныеСотрудникомРаботыОбороты
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВКМ_УсловияОплатыСотрудниковСрезПоследних.Сотрудник КАК Сотрудник,
		|	ВКМ_УсловияОплатыСотрудниковСрезПоследних.Оклад КАК Оклад
		|ПОМЕСТИТЬ ВТ_Оклад
		|ИЗ
		|	РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(&КонецПериода,) КАК
		|		ВКМ_УсловияОплатыСотрудниковСрезПоследних
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(ВТ_Оклад.Сотрудник, ВТ_ОплатаРабот.Сотрудник) КАК Сотрудник,
		|	ЕСТЬNULL(ВТ_Оклад.Оклад, 0) КАК Оклад,
		|	ЕСТЬNULL(ВТ_ОплатаРабот.СуммаКОплатеОборот, 0) КАК СуммаКОплатеОборот
		|ИЗ
		|	ВТ_Оклад КАК ВТ_Оклад
		|		ПОЛНОЕ СОЕДИНЕНИЕ ВТ_ОплатаРабот КАК ВТ_ОплатаРабот
		|		ПО ВТ_Оклад.Сотрудник = ВТ_ОплатаРабот.Сотрудник
		|
		|УПОРЯДОЧИТЬ ПО
		|	Сотрудник";
	
	НачалоПериода = НачалоМесяца(НачалоДня(Объект.Дата));
	КонецПериода = КонецМесяца(КонецДня(Объект.Дата));
	
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Объект.Начисления.Очистить();
	Объект.НДФЛ.Очистить();
	
	Пока Выборка.Следующий() Цикл
		Если Выборка.Оклад <> 0 Тогда
			СтрокаНачисления = Объект.Начисления.Добавить();
			СтрокаНачисления.Сотрудник = Выборка.Сотрудник; 
			СтрокаНачисления.ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад;
			СтрокаНачисления.НачалоПериода = НачалоПериода;
			СтрокаНачисления.КонецПериода = КонецПериода;
			СтрокаНачисления.Показатель = Выборка.Оклад;
			СтрокаНачисления.Сумма = 0;
		КонецЕсли;
		Если Выборка.СуммаКОплатеОборот <> 0 Тогда
			СтрокаНачисления = Объект.Начисления.Добавить();
			СтрокаНачисления.Сотрудник = Выборка.Сотрудник; 
			СтрокаНачисления.ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.ПроцентЗаВыполненыеРаботы;
			СтрокаНачисления.НачалоПериода = НачалоПериода;
			СтрокаНачисления.КонецПериода = КонецПериода;
			СтрокаНачисления.Показатель = Выборка.СуммаКОплатеОборот;
			СтрокаНачисления.Сумма = Выборка.СуммаКОплатеОборот;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

