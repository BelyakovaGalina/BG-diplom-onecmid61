
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЭтоАдресВременногоХранилища(Параметры.Адрес) Тогда 
		ОтпускаСотрудников = ПолучитьИзВременногоХранилища(Параметры.Адрес);
		Если ТипЗнч(ОтпускаСотрудников) = Тип("ТаблицаЗначений") Тогда
			ЗаполнитьДиаграмму(ОтпускаСотрудников);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьДиаграмму(ОтпускаСотрудников)
	
	Графики.Обновление = Ложь;
	Графики.Очистить();

	ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск;
	
	Серия = Графики.УстановитьСерию(ВидРасчета);
	Серия.Текст = ВидРасчета; 

	Для Каждого ОтпускСотрудника Из ОтпускаСотрудников Цикл
		
		Точка = Графики.УстановитьТочку(ОтпускСотрудника.Сотрудник);
		Точка.Текст = ОтпускСотрудника.Сотрудник;
		
		Значение = Графики.ПолучитьЗначение(Точка, Серия);
		Интервал = Значение.Добавить();
		Интервал.Начало = ОтпускСотрудника.ДатаНачала;
		Интервал.Конец = ОтпускСотрудника.ДатаОкончания;
		
	КонецЦикла;

	Графики.Обновление = Истина;
	
КонецПроцедуры

#КонецОбласти
