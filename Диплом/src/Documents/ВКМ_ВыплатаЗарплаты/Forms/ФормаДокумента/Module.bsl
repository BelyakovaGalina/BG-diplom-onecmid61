
#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура Заполнить(Команда)
	Если Объект.Выплаты.Количество() > 0 Тогда
		ЗадатьВопросОПродолжении();
	Иначе
		ЗаполнитьНаСервере();
	КонецЕсли;
КонецПроцедуры
#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Асинх Процедура ЗадатьВопросОПродолжении()
	
	ТекстВопроса = НСтр("ru = 'Заполнение уже было осуществлено. Перезаписать?'"); 
	Ответ = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет);

    Если Ответ = КодВозвратаДиалога.Да Тогда
    	ЗаполнитьНаСервере();
    КонецЕсли;
    
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНаСервере()

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_ВзаиморасчетыССотрудникамиОстатки.Сотрудник КАК Сотрудник,
		|	ВКМ_ВзаиморасчетыССотрудникамиОстатки.СуммаОстаток КАК СуммаОстаток
		|ИЗ
		|	РегистрНакопления.ВКМ_ВзаиморасчетыССотрудниками.Остатки(&Дата,) КАК ВКМ_ВзаиморасчетыССотрудникамиОстатки
		|ГДЕ
		|	ВКМ_ВзаиморасчетыССотрудникамиОстатки.СуммаОстаток > 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	Сотрудник";	

	Запрос.УстановитьПараметр("Дата", КонецДня(Объект.Дата));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Объект.Выплаты.Очистить();
	
	Пока Выборка.Следующий() Цикл
		СтрокаВыплаты = Объект.Выплаты.Добавить();
		СтрокаВыплаты.Сотрудник = Выборка.Сотрудник; 
		СтрокаВыплаты.Сумма = Выборка.СуммаОстаток;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

